buildscript {
    repositories {
        google()
        mavenCentral()
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath libs.android.tools.build
        classpath libs.android.dexcount
        classpath libs.kotlin.gradle.plugin
        classpath libs.kotlin.serialization.plugin
        classpath libs.kotlin.dokka.plugin
    }
}

plugins {
    alias(libs.plugins.kotlin.multiplatform)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.jb.compose)
}

apply plugin: "com.android.library"

repositories {
    mavenLocal()
    mavenCentral()
    google()
}

apply from: "./extensions.gradle"
// apply from: "./github_release.gradle"

project.version = "$version"
project.group = "$group"

// apply from: "$publishGradlePath"

kotlin {
    jvm {
        compilations.main {
            kotlinOptions {
                jvmTarget = "17"
            }
        }
    }
    js (IR) {
        browser()
        nodejs()
    }
    androidTarget {
        publishAllLibraryVariants()
        compilations.all {
            kotlinOptions {
                jvmTarget = "17"
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation libs.kotlin
                api libs.kotlin.serialization
                api libs.krontab
                api libs.navigation
                api(compose.runtime)
            }
        }
        commonTest {
            dependencies {
                implementation libs.kotlin.test.common
                implementation libs.kotlin.test.annotations.common
            }
        }
        jvmMain {
            dependencies {
                implementation(compose.desktop.currentOs)
                api libs.jb.compose.material3.desktop
            }
        }
        jvmTest {
            dependencies {
                implementation libs.kotlin.test.junit
            }
        }
        jsMain {
            dependencies {
                implementation(compose.web.core)
//                api libs.jb.compose.material3.js
            }
        }
        jsTest {
            dependencies {
                implementation libs.kotlin.test.js
                implementation libs.kotlin.test.junit
            }
        }
        androidMain {
            dependencies {
            }
        }
        androidUnitTest {
            dependencies {
                implementation libs.kotlin.test.junit
                implementation libs.android.test.junit
                implementation libs.android.test.espresso.core
            }
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

compose {
    desktop {
        application {
            mainClass = "dev.inmo.krontab.predictor.AppKt"
        }
    }
}

apply from: "$defaultAndroidSettingsPresetPath"
